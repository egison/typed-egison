;;
;; database/edge.egi
;;

(define $node-table
  (matcher
    {[<cons <node ,$px ,$py> $> [node-table]
      {[$tgt (match (pure-sqlite (database-name tgt) (simple-select {"id"} (table-name tgt) {["id" (itos px)]})) (list [integer integer])
               {[<nil> {}]
                [_ {tgt}]})]}]
     [<cons <node ,$px $> $> [integer node-table]
      {[$tgt (map (lambda [$x] [x tgt]) (pure-sqlite (database-name tgt) (simple-select {"name"} (table-name tgt) {["id" (itos px)]})))]}]
     [<cons <node $ ,$px> $> [integer node-table]
      {[$tgt (map (lambda [$x] [(stoi x) tgt]) (pure-sqlite (database-name tgt) (simple-select {"id"} (table-name tgt) {["name" px]})))]}]
     [$ [something]
      {[$tgt {tgt}]}]
     }))

(define $edge-table
  (matcher
    {[<cons <edge ,$px ,$py> $> [edge-table]
      ; select * from edge where from_id = px and to_id = py
      {[$tgt (match (pure-sqlite (database-name tgt) (simple-select {"from_id to_id"} (table-name tgt) {["from_id" (itos px)] ["to_id" (itos py)]})) (list [integer integer])
               {[<nil> {}]
                [_ {tgt}]})]}]
     [<cons <edge ,$px $> $> [integer edge-table]
      ; select to_id from edge where from_id = px
      {[$tgt (map (lambda [$x] [(stoi x) tgt]) (pure-sqlite (database-name tgt) (simple-select {"to_id"} (table-name tgt) {["from_id" (itos px)]})))]}]
     [<cons <edge $ ,$px> $> [integer edge-table]
      ; select from_id from edge where to_id = px
      {[$tgt (map (lambda [$x] [(stoi x) tgt]) (pure-sqlite (database-name tgt) (simple-select {"from_id"} (table-name tgt) {["to_id" (itos px)]})))]}]
     [$ [something]
      {[$tgt {tgt}]}]
     }))

(define $node-data <Database-table "sqlite/graph" "node">)

(define $edge-data <Database-table "sqlite/graph" "edge">)

; two edges from the start node
(test (match-all edge-data edge-table
        [<cons <edge ,40 $m>
               <cons <edge ,m $n>
                     _>>
         [40 m n]]))

; both following
(test (match-all edge-data edge-table
        [<cons <edge ,40 $m>
               <cons <edge ,m ,40>
                     _>>
         [40 m]]))

; not followed back
(test (match-all edge-data edge-table
        [<cons <edge ,40 $m>
               !<cons <edge ,m ,40>
                     _>>
         [40 m]]))

